
; haribote-os boot asm
; TAB=4

BOTPAK	EQU		0x00280000		; bootpack????[?h??
DSKCAC	EQU		0x00100000		; ?f?B?X?N?L???b?V?????
DSKCAC0	EQU		0x00008000		; ?f?B?X?N?L???b?V??????i???A?????[?h?j

; BOOT_INFO??W
CYLS	EQU		0x0ff0			; ?u?[?g?Z?N?^???????
LEDS	EQU		0x0ff1
VMODE	EQU		0x0ff2			; ?F??????????B???r?b?g?J???[???H
SCRNX	EQU		0x0ff4			; ??x??X
SCRNY	EQU		0x0ff6			; ??x??Y
VRAM	EQU		0x0ff8			; ?O???t?B?b?N?o?b?t?@??J?n??n

		ORG		0xc200			; ????v???O????????????????????

; ?????[?h????

		MOV		AL,0x13			; VGA?O???t?B?b?N?X?A320x200x8bit?J???[
		MOV		AH,0x00
		INT		0x10
		MOV		BYTE [VMODE],8	; ?????[?h??????????iC?????Q?????j
		MOV		WORD [SCRNX],320
		MOV		WORD [SCRNY],200
		MOV		DWORD [VRAM],0x000a0000

; ?L?[?{?[?h??LED????BIOS??????????

		MOV		AH,0x02
		INT		0x16 			; keyboard BIOS
		MOV		[LEDS],AL

; PIC??????????????t????????????
;	AT????@??d?l???APIC???????????????A
;	???????CLI?O??????????????A?????n???O?A?b?v????
;	PIC???????????????

		MOV		AL,0xff
		OUT		0x21,AL
		NOP						; OUT?????A?????????????????????@????????????
		OUT		0xa1,AL

		CLI						; ?????CPU???x????????????~

; CPU????1MB???????????A?N?Z?X????????AA20GATE????

		CALL	waitkbdout
		MOV		AL,0xd1
		OUT		0x64,AL
		CALL	waitkbdout
		MOV		AL,0xdf			; enable A20
		OUT		0x60,AL
		CALL	waitkbdout

; ?v???e?N?g???[?h??s

		LGDT	[GDTR0]			; ?b??GDT????
		MOV		EAX,CR0
		AND		EAX,0x7fffffff	; bit31??0?????i?y?[?W???O??~?????j
		OR		EAX,0x00000001	; bit0??1?????i?v???e?N?g???[?h??s?????j
		MOV		CR0,EAX
		JMP		pipelineflush
pipelineflush:
		MOV		AX,1*8			;  ????????\?Z?O?????g32bit
		MOV		DS,AX
		MOV		ES,AX
		MOV		FS,AX
		MOV		GS,AX
		MOV		SS,AX

; bootpack??]??

		MOV		ESI,bootpack	; ?]????
		MOV		EDI,BOTPAK		; ?]????
		MOV		ECX,512*1024/4
		CALL	memcpy

; ??????f?B?X?N?f?[?^???{?????u??]??

; ?????u?[?g?Z?N?^????

		MOV		ESI,0x7c00		; ?]????
		MOV		EDI,DSKCAC		; ?]????
		MOV		ECX,512/4
		CALL	memcpy

; ?c??S??

		MOV		ESI,DSKCAC0+512	; ?]????
		MOV		EDI,DSKCAC+512	; ?]????
		MOV		ECX,0
		MOV		CL,BYTE [CYLS]
		IMUL	ECX,512*18*2/4	; ?V?????_??????o?C?g??/4????
		SUB		ECX,512/4		; IPL???????????????
		CALL	memcpy

; asmhead????????????????????S?????I????????A
;	?????bootpack??C????

; bootpack??N??

		MOV		EBX,BOTPAK
		MOV		ECX,[EBX+16]
		ADD		ECX,3			; ECX += 3;
		SHR		ECX,2			; ECX /= 4;
		JZ		skip			; ?]????????????????
		MOV		ESI,[EBX+20]	; ?]????
		ADD		ESI,EBX
		MOV		EDI,[EBX+12]	; ?]????
		CALL	memcpy
skip:
		MOV		ESP,[EBX+12]	; ?X?^?b?N?????l
		JMP		DWORD 2*8:0x0000001b

waitkbdout:
		IN		 AL,0x64
		AND		 AL,0x02
		JNZ		waitkbdout		; AND??????0???????waitkbdout??
		RET

memcpy:
		MOV		EAX,[ESI]
		ADD		ESI,4
		MOV		[EDI],EAX
		ADD		EDI,4
		SUB		ECX,1
		JNZ		memcpy			; ?????Z?????????0???????memcpy??
		RET
; memcpy??A?h???X?T?C?Y?v???t?B?N?X?????Y???????A?X?g?????O????????????

		ALIGNB	16
GDT0:
		times 8 db 0 			; ?k???Z???N?^
		DW		0xffff,0x0000,0x9200,0x00cf	; ????????\?Z?O?????g32bit
		DW		0xffff,0x0000,0x9a28,0x0047	; ???s??\?Z?O?????g32bit?ibootpack?p?j

		DW		0
GDTR0:
		DW		8*3-1
		DD		GDT0

		ALIGNB	16
bootpack:
